plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
}
apply from: "$rootDir/buildsystem/signingRelease.gradle"
apply from: "$rootDir/buildsystem/androidCommon.gradle"
android {
    defaultConfig {
        applicationId = 'org.illegaller.ratabb.hishoot2i'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
        def fileAuthority = applicationId + '.FILE_AUTHORITY'
        def imageReceiverKey = applicationId + '.IMAGE_RECEIVER'
        buildConfigField 'String', 'FILE_AUTHORITY', "\"$fileAuthority\""
        buildConfigField 'String', 'IMAGE_RECEIVER', "\"$imageReceiverKey\""
        manifestPlaceholders = ["file_authority": fileAuthority, "image_receiver_key": imageReceiverKey]
        resConfigs 'en', 'xxhdpi'
        dependenciesInfo.includeInApk = false
    }
    packagingOptions {
        exclude 'kotlin/**'
        exclude '**/*.kotlin_metadata'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
        exclude 'META-INF/*.properties'
        exclude 'androidsupportmultidexversion.txt'
        exclude 'DebugProbesKt.bin'
    }
    buildTypes {
        release {
            debuggable = false
            shrinkResources = true
            minifyEnabled = true
            zipAlignEnabled = true
            proguardFiles(getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-app.pro')
            signingConfig = signingConfigs.release
        }
    }
    buildFeatures.viewBinding = true
    compileOptions.coreLibraryDesugaringEnabled = true
    applicationVariants.all { variant ->
        def runtimeConf = variant.getRuntimeConfiguration()
        runtimeConf.exclude group: 'net.devrieze', module: 'xmlutil-serialization-jvm'
        runtimeConf.exclude group: 'net.devrieze', module: 'xmlutil-jvm'
    }
}

dependencies {
    implementation project(':common')
    implementation project(':imageloader')
    implementation project(':template')
    implementation project(':pref')
    implementation project(':entity')
    implementation(project(':template-base')) {
        transitive false
    }

    compileOnly androidxLibs.annotation
    implementation androidxLibs.bundles.appCommon
    implementation androidxLibs.bundles.appNav
    implementation androidxLibs.bundles.appLifecycle

    implementation libs.bundles.injectApp
    kapt libs.daggerHiltAndroidCompiler
    implementation androidxLibs.hiltLifecycleViewmodel

    implementation libs.kotlinStd
    coreLibraryDesugaring libs.desugar
    implementation libs.googleMaterial
    implementation libs.timber
    implementation libs.customActivityOnCrash
    implementation libs.leakcanaryPlumberAndroid
    debugImplementation libs.leakcanaryAndroid
    implementation libs.coroutinesAndroid
    implementation libs.coroutinesCoreJvm
}
apply from: "$rootDir/buildsystem/appVersioning.gradle"
