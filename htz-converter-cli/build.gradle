plugins {
    id 'org.jetbrains.kotlin.jvm'
}

dependencies {
    implementation(projects.entity)
    implementation(projects.template.base)
    testImplementation testLibs.bundles.localTest
}

def version = '0.0.1'
def mainClass = 'MainCli'

jar {
    manifest {
        attributes 'Main-Class': mainClass
        attributes 'Implementation-Version': version
    }
}

def fatJarProvider = tasks.register('fatJar', Jar) { task ->
    task.dependsOn(configurations.named('runtimeClasspath'))
    task.dependsOn(tasks.named('jar'))
    task.classifier 'fat'
    task.manifest {
        attributes 'Main-Class': mainClass
        attributes 'Implementation-Version': version
    }
    def sourceClasses = sourceSets.main.output.classesDirs
    task.inputs.files(sourceClasses)

    task.doFirst {
        task.from files(sourceClasses)
        task.from configurations.runtimeClasspath.asFileTree.files.collect { zipTree(it) }
        task.exclude '**/*.kotlin_metadata'
        task.exclude '**/*.kotlin_module'
        task.exclude '**/*.kotlin_builtins'
        task.exclude '**/module-info.class'
        task.exclude 'META-INF/maven/**'
        task.exclude 'META-INF/services/**'
        task.exclude 'META-INF/*.version'
        task.exclude '**/*.proto'
        task.exclude 'LICENSE'
        task.exclude 'NOTICE'
    }
}

def binaryFile = new File(buildDir, "libs/$archivesBaseName-binary.jar")
def binaryJar = tasks.register('binaryCliJar') { task ->
    task.dependsOn(fatJarProvider)
    def fatJar = fatJarProvider.get()
    def fatJarFile = fatJar.archivePath
    task.inputs.file(fatJarFile)
    task.outputs.file(binaryFile)

    task.doLast {
        binaryFile.getParentFile().mkdirs()
        binaryFile.delete()
        binaryFile << "#!/bin/sh\n\nexec java \$JAVA_OPTS -jar \$0 \"\$@\"\n\n"
        fatJarFile.withInputStream { binaryFile.append it }
        binaryFile.setExecutable true, false
    }
}

artifacts {
    archives file: binaryFile, name: 'binary', type: 'jar', builtBy: binaryJar, classifier: 'htz-converter-cli'
}
